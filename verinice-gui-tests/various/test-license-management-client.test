--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _HQDeEI9NEeic2YXgdvyDUQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test-license-management-client
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _8rWy4P1wEeqYO5h9EZ3cUQ
Runtime-Version: 2.5.1.M3
Save-Time: 1/18/21, 11:17 AM
Tags: ISO, clientOnlyTest
Testcase-Type: ecl
Verifications: _wm5P0CtBEeuMltfUUtdvug

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Test the license management on the client by importing encrypted content, add the license and check decryption.
Remove the license.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

global [val licenseDate "2031-01-01"]
global [val isOSX [get-java-property "org.osgi.framework.os.name" | eq "MacOSX"]]

proc "license-file" {
	uri-from-path [concat $basedir [get-java-property "file.separator"]
                           "wrks" [get-java-property "file.separator"]
                           "verinice-workspace" [get-java-property "file.separator"] 
                           "vnl" [get-java-property "file.separator"] 
                           "MY_LIC.vnl"]
}

log "test-license-management"

ism-import-vna  MY_LIC.vna

// the the encrypted content
get-button Objektbrowser | click 
get-view ISM | get-tree | select "importierte Objekte/MyLic [64522f]/Controls [64522f]/Control [64522f]"
screen-shot-window "lic-content-encrypted.png"
get-view Objektbrowser | get-control Browser | get-property text | contains "keine Lizenz zugewiesen" | verify-true

get-view Objektbrowser | close

log [concat "write license file:" [license-file]]
read-file-in-data-dir "MY_LIC.vnl"|
write-lines -uri [license-file]   -append false

// on Mac OSX, the file watcher only polls every 10 seconds, so give it time to pick up the new file
// see https://bugs.openjdk.java.net/browse/JDK-7133447
// https://stackoverflow.com/questions/9588737/is-java-7-watchservice-slow-for-anyone-else
if $isOSX {
	wait 10000
}

get-button Objektbrowser | click 
get-view ISM | get-tree | select "importierte Objekte/MyLic [64522f]/Controls [64522f]/Control [64522f]"
screen-shot-window "lic-content-decrypted.png"
get-view Objektbrowser | get-control Browser | get-property text | contains "keine Lizenz zugewiesen"
    | verify-false

get-view Objektbrowser | close
log [concat "delete license file:" [license-file]]
delete-file [license-file]

//see above
if $isOSX {
	wait 10000
}

get-button Objektbrowser | click 
get-view ISM | get-tree | select "importierte Objekte/MyLic [64522f]/Controls [64522f]/Control [64522f]"
screen-shot-window "lic-content-encrypted-again.png"
get-view Objektbrowser | get-control Browser | get-property text | contains "keine Lizenz zugewiesen"
    | verify-true


delete-iso-model "importierte Objekte/MyLic [64522f]"
log "end-license-management"

verify-error {get-window Fehler }

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
