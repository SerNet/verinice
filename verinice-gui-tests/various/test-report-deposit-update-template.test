--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _HQDeEI9NEeic2YXgdvyDUQ,_lGnj8Ix1Eei2UKUDmonytQ,_mCQwkI9ZEeic2YXgdvyDUQ
Element-Name: test-report-deposit-update-template
Element-Type: testcase
Element-Version: 3.0
External-Reference: https://bob.sernet.private:8443/job/verinice/job/develop/504/artifact/doc/manual/en/verinice-user_manual_EN.pdf#section.14.4
Id: _uxb4QCJoEeuxFrvd53zLWC
Runtime-Version: 2.5.1.M3
Save-Time: 9/6/21, 10:17 AM
Tags: ISO, serverOnlyTest
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Test the update functionality of the report deposit.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

global [val reportName "ITGS: A.1 Strukturanalyse"]
       [val propertiesFileUrl
           [ uri-from-path [concat $basedir [get-java-property "file.separator"]
                           "wrks" [get-java-property "file.separator"]
                           "aut-server-workspace-" [get-java-property "file.separator"]
                           "report_templates_remote" [get-java-property "file.separator"]
                           "bp-A1-structural-analysis_de.properties" ]
           ]
       ]
       [val reportFileUrl
           [ uri-from-path [concat $basedir [get-java-property "file.separator"]
                           "wrks" [get-java-property "file.separator"]
                           "aut-server-workspace-" [get-java-property "file.separator"]
                           "report_templates_remote" [get-java-property "file.separator"]
                           "bp-A1-structural-analysis.rptdesign" ]
           ]
       ]
       [val newReportFileUrl
           [ uri-from-data-dir "Abhaengigkeiten.rptdesign"]
       ]
       

proc "verify-out-format-true" [val formatName]{
    with [get-window "Report editieren"] {
        with [get-group Ausgabeformate] {
            with [get-button $formatName] {
                get-property enablement | equals true | verify-true
                get-property caption | equals $formatName | verify-true
                get-property selected | equals true | verify-true
            }
        }
    }
}

log "report-deposit-update-templatet"

get-button "Report-Ablage" | click
get-view "Report-Ablage" | get-table | select $reportName | double-click

list PDF HTML DOC ODT | each [val format] {  verify-out-format-true $format }

with [get-window "Report editieren"] {
    with [get-button "Alle Scopes einbeziehen (Wird nicht von allen Reports unterst체tzt.)"] {
        get-property enablement | equals true | verify-true
        get-property selected | equals false | verify-true
    }
    with [get-editbox -after [get-label "Template-Datei"]] {
        get-property readOnly | equals true | verify-true
        get-property enablement | equals true | verify-true
        get-property text | equals "bp-A1-structural-analysis.rptdesign" | verify-true
    }
    with [get-editbox -after [get-label "Report-Name"]] {
        get-property enablement | equals true | verify-true
        get-property text | equals $reportName | verify-true
    }
    with [get-control Any -index 4] {
        get-property "getChildren().Control[2].getText()" | equals Kontext | verify-true
        get-property "getChildren().Control[3].getText()" | equals ITGS | verify-true
        get-property "getChildren().Control[3].getItems().String[0]" | equals "ISM-ISO" | verify-true
        get-property "getChildren().Control[3].getItems().String[1]" | equals "ISM-ISA" | verify-true
        get-property "getChildren().Control[3].getItems().String[2]" | equals "ISM-DS" | verify-true
        get-property "getChildren().Control[3].getItems().String[3]" | equals ITGS | verify-true
        get-property "getChildren().Control[3].getItems().String[4]" | equals "ITGS-DS" | verify-true
        get-property "getChildren().Control[3].getItems().String[5]" | equals "ITGS-alt" | verify-true
        get-property "getChildren().Control[3].getItems().String[6]" | equals UNSPECIFIED | verify-true
        get-property "getChildren().Control[3].getSelectionIndex()" | equals 3 | verify-true
    }
    close
}

log $propertiesFileUrl
read-lines -uri $propertiesFileUrl | log
read-properties $propertiesFileUrl | each [val v] [val k] {
    log [concat $k " = " $v ]
}

//now we change data: outputformat
get-view "Report-Ablage" | get-table | select $reportName | double-click
with [get-window "Report editieren"] {
    with [get-group Ausgabeformate] {
        get-button XLS | uncheck
        get-button DOC | uncheck
        get-button ODS | check
    }
    get-button Speichern | click 
}

log "output format changed"
read-properties $propertiesFileUrl | each [val v] [val k] {
    log [concat $k " = " $v ]
}

get-view "Report-Ablage" | get-table | get-item -path $reportName | get-property "columns[2]"
    | equals "HTML, PDF, ODT, ODS" | verify-true

read-properties $propertiesFileUrl | get "outputformats" | eq "HTML,PDF,ODT,ODS" | verify-true

//now we change data: kontext
get-view "Report-Ablage" | get-table | select $reportName | double-click
with [get-window "Report editieren"] {
    get-combo -after [get-label Kontext] | select UNSPECIFIED
    with [get-button Speichern]{
        get-property enablement | equals true | verify-true
        click
    }
}

log "context changed"
read-properties $propertiesFileUrl | each [val v] [val k] {
    log [concat $k " = " $v ]
}

with [get-view "Report-Ablage" | get-table| get-item -path $reportName] {
    get-property caption | equals $reportName | verify-true
    get-property "values['Kontext']" | equals UNSPECIFIED | verify-true
}

get-view "Report-Ablage" | get-table | select $reportName | double-click
with [get-window "Report editieren"] {
    get-control Any -index 4 | get-property "getChildren().Control[3].getText()"
    | equals UNSPECIFIED | verify-true
    get-button Abbrechen | click
}
get-view "Report-Ablage" | get-button Aktualisieren | click

//check the property
read-properties $propertiesFileUrl | get "context" | eq "UNSPECIFIED" | assert-true

//change the report to an existing report
get-button "Report-Ablage" | click
get-view "Report-Ablage" | get-table | select $reportName | double-click

set-dialog-result File $newReportFileUrl
get-window "Report editieren" | get-button "Ausw채hlen" | click 
with [get-window "Report editieren"] {
    get-button Speichern | get-property enablement | equals false | verify-true
    get-label "Report-Template existiert als 'Abhaengigkeiten.rptdesign' auf dem Server, bitte w채hlen Sie eine andere Datei."
}
screen-shot "Report editieren" "add_report_file-error-exist.png"
// and now back to the old name, and change the values back
set-dialog-result File $reportFileUrl
with [get-window "Report editieren"] {
    get-button "Ausw채hlen" | click 
    get-button Speichern | get-property enablement | equals true | verify-true
    with [get-group Ausgabeformate] {
        get-button DOC | check
        get-button ODS | uncheck
    }
    get-combo -after [get-label Kontext] | select ITGS
    get-button Speichern | click 
}
//now check the view
with [get-view "Report-Ablage" | get-table] {
    get-item -path "ITGS: A.1 Strukturanalyse" | get-property "columns[0]" | equals "ITGS: A.1 Strukturanalyse" 
        | verify-true
    get-item -path "ITGS: A.1 Strukturanalyse" | get-property "columns[1]" | equals ITGS | verify-true
    get-item -path "ITGS: A.1 Strukturanalyse" | get-property "columns[2]" | equals "HTML, DOC, PDF, ODT" | verify-true
    get-item -path "ITGS: A.1 Strukturanalyse" | get-property "columns[3]" 
        | equals "bp-A1-structural-analysis.rptdesign" | verify-true
}
//and the property file
read-properties $propertiesFileUrl | each [val v] [val k] {
    log [concat $k " = " $v ]
}
read-properties $propertiesFileUrl | get "outputformats" | eq "HTML,DOC,PDF,ODT" | verify-true

log "report-deposit-update-template"
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
